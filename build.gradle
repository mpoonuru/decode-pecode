plugins {
    id "com.jfrog.bintray" version "1.8.4"
    id 'java'
}

repositories {
    jcenter()
}

group 'decodepcode'
version '0.0.6'

allprojects {
    tasks.withType(JavaCompile) {
        sourceCompatibility = '1.7'
        targetCompatibility = '1.7'
    }
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
}

// Include dependent libraries in archive.

task fatJar(type: Jar) {
    classifier = 'fat'
    manifest {
        attributes "Main-Class": "decodepcode.Controller"
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.MF'
    }
    with jar
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    javadoc.options.encoding = 'UTF-8'
    from javadoc.destinationDir
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'decodepcode'
            artifactId 'DecodePcode'
            version this.version

            artifact sourceJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }

            artifact fatJar {
                classifier "fat"
            }

        }
    }
}

bintray{
    user=System.getenv('BINTRAY_USER')
    key=System.getenv('BINTRAY_API_KEY')
//    configurations = ['archives']
    publications = ['MyPublication']
    pkg {
        repo = 'Maven'
        name = 'Decode-Pcode'
        licenses = ['ISC']
        websiteUrl = 'https://github.com/techops-bob/decode-pecode'
        issueTrackerUrl = 'https://github.com/techops-bob/decode-pecode/issues'
        desc = 'An opensource library to backup PeopleSoft-Code to your desired source control systems.'
        vcsUrl = 'https://github.com/techops-bob/decode-pecode.git'
        githubRepo = 'techops-bob/decode-pecode'
        githubReleaseNotesFile = 'README.md'
        labels = ['VCS', 'PeopleSoft']
        publicDownloadNumbers = true

        version {
            released  = new Date()
            name = this.version
            desc = 'Published PeopleSoft version control tool'
            vcsTag = this.version
        }

    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile 'org.eclipse.jgit:org.eclipse.jgit:5.5.0.201909110433-r'
    compile 'org.tmatesoft.svnkit:svnkit:1.10.1'
    compile 'com.jslsolucoes:ojdbc6:11.2.0.1.0'
    implementation 'com.google.code.gson:gson:2.8.6'
}
